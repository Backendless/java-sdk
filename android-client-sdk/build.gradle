plugins {
  id 'com.android.library' version '7.4.0-beta02'
}

group 'com.backendless'
archivesBaseName = 'android-client-sdk'
version property('version.android-client-sdk')

dependencies {
  implementation project( ':java-sdk-common' )

  implementation( group: "com.backendless", name: "commons", version: property( 'version.backendless-commons' ) ) {
    exclude group: 'com.fasterxml.jackson.core'
  }
  implementation( group: "com.backendless", name: "weborbclient", version: property( 'version.weborbclient' ) )

  compileOnly( "io.socket:socket.io-client:${property( 'version.io-socket-client' )}" ) {
//       excluding org.json which is provided by Android
    exclude group: 'org.json', module: 'json'
  }

  compileOnly( "org.projectlombok:lombok:${property( 'version.projectlombok' )}" )
  annotationProcessor( "org.projectlombok:lombok:${property( 'version.projectlombok' )}" )

  testImplementation( "org.junit.jupiter:junit-jupiter-api:${property( 'version.junit-jupiter' )}" )
  testRuntimeOnly( "org.junit.jupiter:junit-jupiter-engine:${property( 'version.junit-jupiter' )}" )

  //compileOnly 'androidx.appcompat:appcompat:1.4.1'
  compileOnly 'com.android.support:support-compat:28.0.0'
  compileOnly 'com.google.firebase:firebase-messaging:23.1.1'
  compileOnly 'com.google.firebase:firebase-iid:21.1.0'

  testImplementation( "org.junit.jupiter:junit-jupiter-api:${property( 'version.junit-jupiter' )}" )
  testRuntimeOnly( "org.junit.jupiter:junit-jupiter-engine:${property( 'version.junit-jupiter' )}" )

  // Required -- JUnit 4 framework
  //testImplementation "junit:junit:$jUnitVersion" // 4.13.2
  // Optional -- Robolectric environment
  //testImplementation "androidx.test:core:$androidXTestVersion"
  //androidTestImplementation 'androidx.test.ext:junit:1.1.3'
  //androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
  // Optional -- Mockito framework
  //testImplementation "org.mockito:mockito-core:$mockitoVersion"
  // Optional -- mockito-kotlin
  //testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
  // Optional -- Mockk framework
  //testImplementation "io.mockk:mockk:$mockkVersion"
}

android {
  namespace 'com.backendless'
  compileSdk 33
  buildToolsVersion "33.0.2"
  defaultConfig {
    minSdk 27
    targetSdk 33
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  lint {
    abortOnError false
//    baseline = file( project.buildDir.toPath(  ).resolve( "lint-baseline.xml") )
    baseline = file( "lint-baseline.xml" )
  }

  buildTypes {
    release {
//            minifyEnabled false
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  libraryVariants.all { variant ->
    variant.outputs.all {
      outputFileName = "${archivesBaseName}-${defaultConfig.versionName}-${variant.name}.aar"
    }
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  publishing {
    multipleVariants ( 'all' ) {
      includeBuildTypeValues('release')
      //allVariants()
      withSourcesJar()
      withJavadocJar()
    }
  }
}



publishing {
  publications {
    mavenJava( MavenPublication ) {
      //from components.all
      afterEvaluate {
        from components.all
      }

      pom {
        // optionally
        /*
        groupId = 'com.backendless'
        artifactId = 'backendless'
        version = '8.0.0'
        packaging = 'jar'
        */

        name = 'Android-Client-SDK for Backendless'
        description = 'Provides access to Backendless API'
        url = 'https://backendless.com'

        // optionally props
        /*
        properties = [
                myProp: "value",
                "prop.with.dots": "anotherValue"
        ]
        */

        scm {
          connection = 'scm:git:https://github.com/Backendless/java-sdk.git'
          developerConnection = 'scm:git:https://github.com/Backendless/java-sdk.git'
          url = 'https://github.com/Backendless/java-sdk.git'
        }

        licenses {
          license {
            name = 'The MIT License (MIT)'
            url = 'https://opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            id = 'backendless'
            name = 'backendless'
            email = 'info@backednless.com'
          }
        }
      }
    }
  }
}
