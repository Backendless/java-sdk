plugins {
  id 'java-library'
}

group 'com.backendless'
archivesBaseName = 'java-sdk-common'
version property('version.java-sdk-common')

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
  withSourcesJar()
  withJavadocJar()
}

artifacts {
  archives jar, sourcesJar, javadocJar
}

dependencies {
  api( group: "com.backendless", name: "commons", version: property( 'version.backendless-commons' ) )
  api( group: "com.backendless", name: "weborbclient", version: property( 'version.weborbclient' ) )

  compileOnly( "io.socket:socket.io-client:${property( 'version.io-socket-client' )}" )
  compileOnly( "org.projectlombok:lombok:${property( 'version.projectlombok' )}" )
  annotationProcessor( "org.projectlombok:lombok:${property( 'version.projectlombok' )}" )

  testImplementation( "org.junit.jupiter:junit-jupiter-api:${property( 'version.junit-jupiter' )}" )
  testRuntimeOnly( "org.junit.jupiter:junit-jupiter-engine:${property( 'version.junit-jupiter' )}" )
}

println configurations.implementation

parent.copyJarToOut.doLast {
    copy( () -> from jar.archiveFile into "$parent.projectDir.path/out" )
    copy( () -> from configurations.runtimeClasspath into "$parent.projectDir.path/out" )
}

test {
  useJUnitPlatform()
}


publishing {
  publications {
    mavenJava( MavenPublication ) {
      from components.java
      pom {
        // optionally
        /*
        groupId = 'com.backendless'
        artifactId = 'backendless'
        version = '8.0.0'
        */
        packaging = 'jar'

        name = 'Java-SDK (common part) for Backendless'
        description = 'Provides access to Backendless API'
        url = 'https://backendless.com'

        // optionally props
        /*
        properties = [
                myProp: "value",
                "prop.with.dots": "anotherValue"
        ]
        */

        scm {
          connection = 'scm:git:https://github.com/Backendless/java-sdk.git'
          developerConnection = 'scm:git:https://github.com/Backendless/java-sdk.git'
          url = 'https://github.com/Backendless/java-sdk.git'
        }

        licenses {
          license {
            name = 'The MIT License (MIT)'
            url = 'https://opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            id = 'backendless'
            name = 'backendless'
            email = 'info@backednless.com'
          }
        }
      }
    }
  }
}
