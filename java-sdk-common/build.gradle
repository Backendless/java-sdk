plugins {
  id 'java-library'
}

group 'com.backendless'
archivesBaseName = 'java-sdk-common'
version '8.0.0'

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

task sourcesJar( type: Jar ) {
  archiveClassifier.set( "sources" )
  from sourceSets.main.allSource
}

task javadocJar( type: Jar ) {
  archiveClassifier.set( "javadoc" )
  from javadoc
}

artifacts {
  archives sourcesJar, javadocJar
}

dependencies {
  implementation( group: "com.backendless", name: "commons", version: property( 'version-com.backendless-commons' ) ) {
    exclude group: 'com.fasterxml.jackson.core'
  }
  implementation( group: "weborb", name: "weborbclient", version: property( 'version-weborb-weborbclient' ) )

  compileOnly( "io.socket:socket.io-client:${property( 'version-io.socket-client' )}" ) {
//       excluding org.json which is provided by Android
    exclude group: 'org.json', module: 'json'
  }

  compileOnly( "org.projectlombok:lombok:${property( 'version-org.projectlombok' )}" )
  annotationProcessor( "org.projectlombok:lombok:${property( 'version-org.projectlombok' )}" )

  testImplementation( "org.junit.jupiter:junit-jupiter-api:${property( 'version-org.junit.jupiter' )}" )
  testRuntimeOnly( "org.junit.jupiter:junit-jupiter-engine:${property( 'version-org.junit.jupiter' )}" )
}

test {
  useJUnitPlatform()
}


publishing {
  publications {
    mavenJava( MavenPublication ) {
      from components.java
      pom {
        // optionally
        /*
        groupId = 'com.backendless'
        artifactId = 'backendless'
        version = '8.0.0'
        packaging = 'jar'
        */

        name = 'Java-SDK (common part) for Backendless'
        description = 'Provides access to Backendless API'
        url = 'https://backendless.com'

        // optionally props
        /*
        properties = [
                myProp: "value",
                "prop.with.dots": "anotherValue"
        ]
        */

        scm {
          connection = 'scm:git:https://github.com/Backendless/java-sdk.git'
          developerConnection = 'scm:git:https://github.com/Backendless/java-sdk.git'
          url = 'https://github.com/Backendless/java-sdk.git'
        }

        licenses {
          license {
            name = 'The MIT License (MIT)'
            url = 'https://opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            id = 'backendless'
            name = 'backendless'
            email = 'info@backednless.com'
          }
        }
      }
    }
  }
}
